// Custom state id for shooting.
const int ENEMY_OCTOROK_STATE_ATTACK = 100;

// State indices.
const int ENEMY_OCTOROK_ATTACK_PHASE = 9;
const int ENEMY_OCTOROK_ATTACK_TIMER = 10;

//// Attack state phases.
// Phase for the initial start delay.
const int ENEMY_OCTOROK_ATTACK_PHASE_START = 1;
// Phase for the shot being shot.
const int ENEMY_OCTOROK_ATTACK_PHASE_FIRING = 2;
// Phase for the end delay.
const int ENEMY_OCTOROK_ATTACK_PHASE_END = 3;

// Tile foffsets for the different actions.
const int ENEMY_OCTOROK_TILE_STAND = 0;
const int ENEMY_OCTOROK_TILE_WALK = 20;
const int ENEMY_OCTOROK_TILE_SHOT = 40;

// How long to pursue link without seeing him.
const int ENEMY_OCTOROK_PURSUE_DURATION = 240;
// How close to being axis aligned with the target before attacking.
const int ENEMY_OCTOROK_ALIGN_MIN = 4;
const int ENEMY_OCTOROK_ALIGN_MAX = 8;
// How close link can get before you run away.
const int ENEMY_OCTOROK_RUN_MIN = 24;
const int ENEMY_OCTOROK_RUN_MAX = 48;
// Number of frames it takes before shooting.
const int ENEMY_OCTOROK_CHARGE_MIN = 12;
const int ENEMY_OCTOROK_CHARGE_MAX = 18;
// Number of frames the attacking animation lasts.
const int ENEMY_OCTOROK_ATTACK_DURATION = 4;
// How long before we can move again after firing.
const int ENEMY_OCTOROK_RECOVER_MIN = 20;
const int ENEMY_OCTOROK_RECOVER_MAX = 50;
// Sound for attacking
const int ENEMY_OCTOROK_ATTACK_SOUND = 42;

ffc script enemy_octorok {
	void run() {
		int enemy_id = this->InitD[ENEMY_FFCD_ID];
		int npc_index = Npc_ScreenIndex(enemy_id);
		if (!npc_index) {return;}

		npc o = Screen->LoadNPC(npc_index);
		int base_tile = o->OriginalTile;

		// Give default defenses.
		Damage_Fill(Enemy_Data, Enemy_Defense(enemy_id), 3);
		Enemy_Val(enemy_id, ENEMY_DEFENSE + DAMAGE_CRUSH, 11);

		// Give default attack.
		Enemy_Val(enemy_id, ENEMY_ATTACK + DAMAGE_KNOCKBACK, 10);
		Enemy_Val(enemy_id, ENEMY_ATTACK + DAMAGE_CRUSH, 8);

		// Setup detection.
		Enemy_Sight(enemy_id, 210);
		Enemy_SenseLink(enemy_id);

		// wander data array.
		int wander[3];
		// Track the walking animation.
		int walk_timer = 0;

		// Randomize the various values so enemies don't clump as much.
		int align_distance = Rand(
				ENEMY_OCTOROK_ALIGN_MIN,
				ENEMY_OCTOROK_ALIGN_MAX);
		int run_distance = Rand(
				ENEMY_OCTOROK_RUN_MIN,
				ENEMY_OCTOROK_RUN_MAX);
		int charge_time = Rand(
				ENEMY_OCTOROK_CHARGE_MIN,
				ENEMY_OCTOROK_CHARGE_MAX);
		int recover_time = Rand(
				ENEMY_OCTOROK_RECOVER_MIN,
				ENEMY_OCTOROK_RECOVER_MAX);

		while (true) {
			if (!Enemy_Flag(enemy_id, ENEMY_F1, ENEMY_F1_ALLOC)) {
				return;}

			if (!o->isValid()) {
				Enemy_Free(enemy_id);
				return;}

			int tile = base_tile;
			tile += o->Dir;
			int tile_offset = ENEMY_OCTOROK_TILE_STAND;

			int state = Enemy_State(enemy_id);

			// Standard walking around.
			if (ENEMY_STATE_NONE == state) {
				// Walking animation.
				walk_timer = (walk_timer + 1) % 24;
				if (walk_timer >= 12) {tile_offset = ENEMY_OCTOROK_TILE_WALK;}

				int time = Enemy_LinkTime(enemy_id);
				// If we don't see link, then wander.
				if (-1 == time || time > ENEMY_OCTOROK_PURSUE_DURATION) {
					Enemy_Wander(enemy_id, wander);}

				// We can see link.
				else {
					Enemy_Center(enemy_id);
					int ex = R[0];
					int ey = R[1];
					Link_Center();
					int lx = R[0];
					int ly = R[1];
					int dx = lx - ex;
					int dy = ly - ey;
					int dir = Dir_Major(dx, dy);
					int distance = Sqrt(dx*dx + dy*dy);

					// If link is not directly seen, investigate.
					if (0 != time) {
						int dist = Enemy_Walk(enemy_id, lx, ly);
						// If we can't for some reason, we've lost sight of link.
						if (0 == dist) {
							Enemy_LinkTime(enemy_id, -1);}}

					// If link is directly in sight, and too close, run away.
					else if (distance < run_distance) {
						int dist = Enemy_Walk(enemy_id, ex - dx, ey - dy);
						// If we can't flee, turn to shoot instead.
						if (0 == dist) {
							o->Dir = dir;
							Enemy_State(enemy_id, ENEMY_OCTOROK_STATE_ATTACK);
							Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_PHASE, ENEMY_OCTOROK_ATTACK_PHASE_START);
							Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_TIMER, charge_time);}}

					// If link is seen and far enough away, try to line up for a shot.
					else if (Min(Abs(dx), Abs(dy)) > align_distance) {
						// Give a tiny bit of movement away from the target, but mostly
						// moving towards alignment.
						int x = lx;
						int y = ly;
						if (Abs(dx) < Abs(dy)) {
							y = ly * -0.1 + ey * 1.1;}
						else {
							x = lx * -0.1 + ex * 1.1;}
						int dist = Enemy_Walk(enemy_id, x, y);
						// If alignment fails, move directly to shooting.
						if (0 == dist) {
							o->Dir = dir;
							Enemy_State(enemy_id, ENEMY_OCTOROK_STATE_ATTACK);
							Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_PHASE, ENEMY_OCTOROK_ATTACK_PHASE_START);
							Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_TIMER, charge_time);}}

					// We're aligned, so shoot.
					else {
						o->Dir = dir;
						Enemy_State(enemy_id, ENEMY_OCTOROK_STATE_ATTACK);
						Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_PHASE, ENEMY_OCTOROK_ATTACK_PHASE_START);
						Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_TIMER, charge_time);}}}

			// Attacking
			else if (ENEMY_OCTOROK_STATE_ATTACK == state) {
				int phase = Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_PHASE);
				int timer = Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_TIMER);

				--timer;

				if (ENEMY_OCTOROK_ATTACK_PHASE_START == phase) {
					if (timer <= 0) {
						phase = ENEMY_OCTOROK_ATTACK_PHASE_FIRING;
						timer = ENEMY_OCTOROK_ATTACK_DURATION;
						Game->PlaySound(ENEMY_OCTOROK_ATTACK_SOUND);

						lweapon weapon_object = Weapon_Create();
						int weapon_id = LWeapon_Id(weapon_object);
						Weapon_HitLink(weapon_id, true);
						Weapon_Val(weapon_id, WEAPON_DAMAGE + DAMAGE_KNOCKBACK, 12);
						Weapon_Val(weapon_id, WEAPON_DAMAGE + DAMAGE_CRUSH, o->WeaponDamage);
						weapon_object->Dir = o->Dir;
						weapon_object->OriginalTile = base_tile + ENEMY_OCTOROK_TILE_SHOT;
						weapon_object->NumFrames = 1;
						weapon_object->Step = 200;
						Weapon_Size(weapon_id, 8, 8);
						Weapon_Terrain(weapon_id, TERRAIN_FLY | TERRAIN_ONSCREEN);
						Enemy_Center(enemy_id);
						if (DIR_UP == o->Dir) {
							Weapon_Center(weapon_id, R[0], R[1] - 10);}
						else if (DIR_DOWN == o->Dir) {
							Weapon_Center(weapon_id, R[0], R[1] + 10);}
						else if (DIR_LEFT == o->Dir) {
							Weapon_Center(weapon_id, R[0] - 10, R[1]);}
						else if (DIR_RIGHT == o->Dir) {
							Weapon_Center(weapon_id, R[0] + 10, R[1]);}

						o->DrawXOffset = Dir_X(o->Dir) * -2;
						o->DrawYOffset = Dir_Y(o->Dir) * -2;}}

				if (ENEMY_OCTOROK_ATTACK_PHASE_FIRING == phase && timer <= 0) {
					o->DrawXOffset = 0;
					o->DrawYOffset = 0;
					phase = ENEMY_OCTOROK_ATTACK_PHASE_END;
					timer = recover_time;}

				if (ENEMY_OCTOROK_ATTACK_PHASE_END == phase && timer <= 0) {
					Enemy_State(enemy_id, ENEMY_STATE_NONE);
					phase = 0;
					timer = 0;}

				// Write back phase and timer.
				Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_PHASE, phase);
				Enemy_Val(enemy_id, ENEMY_OCTOROK_ATTACK_TIMER, timer);}

			Enemy_Tile(enemy_id, tile + tile_offset);
			Waitframe();}}}
